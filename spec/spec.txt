# Terminals
delim ;
return return
# Control flow
if if
else else
while while
lbrace \{
rbrace \}
assign =
# Arithmetic/bitwise operations
num [0-9]+
ident [a-zA-Z_]+
plus \+
minus \-
multiply \*
divide /
bitand &
bitor \|
bitxor \^
lshift <<
rshift >>
lparen \(
rparen \)
%%
# Precedence and associativity
# <terminal> <precedence:unsigned> <associativity:left|right|none>
# Order inspired by C operator precedence
bitand 0 left
bitor 0 left
bitxor 0 left
lshift 1 left
rshift 1 left
plus 2 left
minus 2 left
multiply 3 left
divide 3 left
lparen 4 left
rparen 4 left
%%
# Non-terminals
Program
FunctionDefinition
FunctionParams
Block
Statement
AssignStatement
ReturnStatement
IfStatement
IfElseStatement
WhileStatement
Expr
%%
# Production rules
Program -> FunctionDefinition
Program -> FunctionDefinition Program
FunctionDefinition -> ident lparen rparen lbrace Block rbrace
FunctionDefinition -> ident lparen FunctionParams rparen lbrace Block rbrace
FunctionParams -> ident
FunctionParams -> FunctionParams ident
Block -> Statement
Block -> Block Statement
Statement -> AssignStatement
Statement -> ReturnStatement
Statement -> IfStatement
Statement -> IfElseStatement
Statement -> WhileStatement
AssignStatement -> ident assign Expr delim
ReturnStatement -> return Expr delim
IfStatement -> if lparen Expr rparen lbrace Block rbrace
IfElseStatement -> if lparen Expr rparen lbrace Block rbrace else lbrace Block rbrace
WhileStatement -> while lparen Expr rparen lbrace Block rbrace
Expr -> num
Expr -> ident
Expr -> minus num
Expr -> lparen Expr rparen
Expr -> Expr plus Expr
Expr -> Expr minus Expr
Expr -> Expr multiply Expr
Expr -> Expr divide Expr
Expr -> Expr bitand Expr
Expr -> Expr bitor Expr
Expr -> Expr bitxor Expr
Expr -> Expr lshift Expr
Expr -> Expr rshift Expr